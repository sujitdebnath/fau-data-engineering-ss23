pipeline AirportsPipeline {

    // Extractor Block: Extracts airports CSV file from the desired url (In: None, Out: File)
	block AirportsExtractor oftype HttpExtractor {
		url: "https://opendata.rhein-kreis-neuss.de/api/v2/catalog/datasets/rhein-kreis-neuss-flughafen-weltweit/exports/csv";
	}

    // Pipe: AirportsExtractor (Out: File) -> AirportsTextFileInterpreter (In: File)
    pipe {
		from: AirportsExtractor;
		to: AirportsTextFileInterpreter;
	}

    // Transformer Block: Converts airports File to TextFile (In: File, Out: TextFile)
    block AirportsTextFileInterpreter oftype TextFileInterpreter {
        encoding: "utf-8";
	}

    // Pipe: AirportsTextFileInterpreter (Out: TextFile) -> AirportsCSVInterpreter (In: TextFile)
	pipe {
		from: AirportsTextFileInterpreter;
		to: AirportsCSVInterpreter;
	}

    // Transformer Block: Converts airports TextFile to Sheet (In: TextFile, Out: Sheet)
    block AirportsCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
	}

    // Pipe: AirportsCSVInterpreter (Out: Sheet) -> AirportsTableInterpreter (In: Sheet)
    pipe {
	   	from: AirportsCSVInterpreter;
	   	to: AirportsTableInterpreter;
	}

    // Transformer Block: Converts airports Sheet to Table (In: Sheet, Out: Table)
	block AirportsTableInterpreter oftype TableInterpreter {
		header: true;
		columns: [
            "column_1" oftype integer,
            "column_2" oftype text,
            "column_3" oftype text,
            "column_4" oftype text,
            "column_5" oftype text,
            "column_6" oftype text,
            "column_7" oftype decimal,
            "column_8" oftype decimal,
            "column_9" oftype integer,
            "column_10" oftype decimal,
            "column_11" oftype text,
            "column_12" oftype text,
            "geo_punkt" oftype text
		];
	}

    // Pipe: AirportsTableInterpreter (Out: Table) -> AirportsLoader (In: Table)
    pipe {
		from: AirportsTableInterpreter;
		to: AirportsLoader;
	}

    // Loader Block: Load airports data into sqlite database (In: Table, Out: None)
	block AirportsLoader oftype SQLiteLoader {
		table: "airports";
		file: "./airports.sqlite";
	}

}